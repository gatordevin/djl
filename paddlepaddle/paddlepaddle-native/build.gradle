plugins {
    id 'maven-publish'
    id 'signing'
}

group "ai.djl.paddlepaddle"

boolean isRelease = project.hasProperty("release") || project.hasProperty("staging")
version = "${paddlepaddle_version}" + (isRelease ? "" : "-SNAPSHOT")

task prepareNativeLibs() {
    doLast {
        delete "${buildDir}/native"

        def files = [
                // "linux/cu101": "",
                // "linux/cpu"  : "",
                "osx/cpu": "https://paddle-inference-lib.bj.bcebos.com/mac/2.0-rc/cpu_avx_openblas/paddle_inference_install_dir.tgz",
                // "win/cu101"  : "",
                "win/cpu": "https://paddle-wheel.bj.bcebos.com/2.0.0-rc0/win-infer/mkl/cpu/paddle_inference_install_dir.zip"
        ]

        def downloadDir = file("${buildDir}/download")
        downloadDir.mkdirs();

        files.each { entry ->
            String url = entry.value;
            String fileName = url.substring(url.lastIndexOf('/') + 1);
            File file = new File(downloadDir, fileName);
            if (!file.exists()) {
                project.logger.lifecycle("Downloading ${entry.key} -> ${url}")
                new URL(url).withInputStream { i -> file.withOutputStream { it << i } }
            }

            project.logger.lifecycle("Extracting native files from: ${fileName}")
            if (fileName.endsWith(".zip")) {
                copy {
                    from {
                        zipTree(file).matching { include "**/*.dll" }
                    }
                    eachFile {
                        path = name
                    }
                    into "${buildDir}/native/${entry.key}/libs"
                }
            } else if (fileName.endsWith(".tgz")) {
                File tmpDir = new File("${buildDir}/native/tmp")
                tmpDir.mkdirs()
                exec {
                    workingDir tmpDir
                    commandLine "sh", "-c", "tar xvfz ${file.absolutePath}"
                }
                copy {
                    from(tmpDir) {
                        include '**/*.dylib'
                        include '**/*.so'
                    }
                    eachFile {
                        path = name
                    }
                    into "${buildDir}/native/${entry.key}/libs"
                }
                delete "${buildDir}/native/tmp"
            }
        }

        exec {
            commandLine "sh", "-c", "find ${buildDir}/native -type f | xargs gzip"
        }

        def f = new File("${buildDir}/native/files.txt")
        def uploadDirs = [
                "${buildDir}/native/linux/cpu/",
                "${buildDir}/native/linux/cu101/",
                "${buildDir}/native/osx/cpu/",
                "${buildDir}/native/win/cpu/",
                "${buildDir}/native/win/cu101/"
        ]
        uploadDirs.each { item ->
            fileTree(item).files.name.each {
                def out = item - "${buildDir}/native/"
                f << out + it
                f << "\n"
            }
        }

        exec {
            commandLine "aws", "s3", "sync", "${buildDir}/native/", "s3://djl-ai/publish/paddlepaddle-${paddlepaddle_version}/"
        }
    }
}

jar {
    // this line is to enforce gradle to build the jar
    // otherwise it don't generate the placeholder jar at times
    // when there is no java code inside src/main
    outputs.dir file("build/libs")
    doFirst {
        def dir = file("${project.buildDir}/classes/java/main/native/lib")
        dir.mkdirs();
        def propFile = new File(dir, "paddlepaddle.properties")
        def versionName = project.version
        if (!isRelease) {
            versionName += String.format("-%s", new Date().format('yyyyMMdd'))
        }
        propFile.text = "placeholder=true\nversion=${project.version}\n"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

project.tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

signing {
    required(project.hasProperty("staging") || project.hasProperty("snapshot"))
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications
}

def BINARY_ROOT = "${project.buildDir}/download"
def flavorNames = file(BINARY_ROOT).list() ?: []
flavorNames.each { flavor ->

    def platformNames = file("${BINARY_ROOT}/${flavor}").list() ?: []

    def artifactsNames = []

    platformNames.each { osName ->
        tasks.create(name: "${flavor}-${osName}Jar", type: Jar) {
            doFirst {
                def propFile = file("${BINARY_ROOT}/${flavor}/${osName}/native/lib/paddlepaddle.properties")
                propFile.delete()
                def dsStore = file("${BINARY_ROOT}/${flavor}/${osName}/native/lib/.DS_Store")
                dsStore.delete()

                def versionName = String.format("${version}-%s", new Date().format('yyyyMMdd'))
                def dir = file("${BINARY_ROOT}/${flavor}/${osName}/native/lib")
                def sb = new StringBuilder()
                sb.append("version=${versionName}\nclassifier=${flavor}-${osName}-x86_64\nlibraries=")
                def first = true
                for (String name : dir.list().sort()) {
                    if (first) {
                        first = false;
                    } else {
                        sb.append(',')
                    }
                    sb.append(name)
                }
                propFile.text = sb.toString()

                from file("src/main/resources")
            }
            from file("${BINARY_ROOT}/${flavor}/${osName}")
            archiveClassifier = "${osName}-x86_64"
        }
        artifactsNames.add(tasks["${flavor}-${osName}Jar"])
    }

    // Only publish if the project directory equals the current directory
    // This means that publishing from the main project does not publish the native jars
    // and the native jars have to be published separately
    if (project.getProjectDir().toString() == System.getProperty("user.dir")) {
        publishing.publications.create("${flavor}", MavenPublication) {
            artifactId "paddlepaddle-native-${flavor}"
            from components.java
            artifacts = artifactsNames
            artifact jar
            artifact javadocJar
            artifact sourcesJar
            pom {
                name = "DJL release for PaddlePaddle native binaries"
                description = "Deep Java Library (DJL) provided PaddlePaddle native library binary distribution"
                url = "http://www.djl.ai/paddlepaddle/${project.name}"
                packaging = "jar"

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                scm {
                    connection = "scm:git:git@github.com:awslabs/djl.git"
                    developerConnection = "scm:git:git@github.com:awslabs/djl.git"
                    url = "https://github.com/awslabs/djl"
                    tag = "HEAD"
                }

                developers {
                    developer {
                        name = "DJL.AI Team"
                        email = "djl-dev@amazon.com"
                        organization = "Amazon AI"
                        organizationUrl = "https://amazon.com"
                    }
                }
            }
        }
    }
}

publishing.repositories {
    maven {
        if (project.hasProperty("snapshot")) {
            name = "snapshot"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
            credentials {
                username = findProperty("ossrhUsername")
                password = findProperty("ossrhPassword")
            }
        } else if (project.hasProperty("staging")) {
            name = "staging"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = findProperty("ossrhUsername")
                password = findProperty("ossrhPassword")
            }
        } else {
            name = "local"
            url = "build/repo"
        }
    }
}


import java.util.zip.GZIPInputStream

task downloadpaddlepaddleNativeLib() {
    doLast {
        def url = "https://djl-ai.s3.amazonaws.com/publish/paddlepaddle-${paddlepaddle_version}"
        def files = [
                "linux/cpu/libdnnl.so.1.gz"            : "cpu/linux/native/lib/libdnnl.so.1",
                "linux/cpu/libiomp5.so.gz"             : "cpu/linux/native/lib/libiomp5.so",
                "linux/cpu/libmkldnn.so.0.gz"          : "cpu/linux/native/lib/libmkldnn.so.0",
                "linux/cpu/libmklml_intel.so.gz"       : "cpu/linux/native/lib/libmklml_intel.so",
                "linux/cpu/libpaddle_framework.so.gz"  : "cpu/linux/native/lib/libpaddle_framework.so",
                "linux/cpu/libwarpctc.so.gz"           : "cpu/linux/native/lib/libwarpctc.so",
                "linux/cu101/libdnnl.so.1.gz"          : "cu101/linux/native/lib/libdnnl.so.1",
                "linux/cu101/libiomp5.so.gz"           : "cu101/linux/native/lib/libiomp5.so",
                "linux/cu101/libmkldnn.so.0.gz"        : "cu101/linux/native/lib/libmkldnn.so.0",
                "linux/cu101/libmklml_intel.so.gz"     : "cu101/linux/native/lib/libmklml_intel.so",
                "linux/cu101/libpaddle_framework.so.gz": "cu101/linux/native/lib/libpaddle_framework.so",
                "linux/cu101/libwarpctc.so.gz"         : "cu101/linux/native/lib/libwarpctc.so",
                "osx/cpu/libwarpctc.dylib.gz"          : "cpu/osx/native/lib/libwarpctc.dylib",
                "win/cpu/libiomp5md.dll.gz"            : "cpu/win/native/lib/libiomp5md.dll",
                "win/cpu/mkldnn.dll.gz"                : "cpu/win/native/lib/mkldnn.dll",
                "win/cpu/mklml.dll.gz"                 : "cpu/win/native/lib/mklml.dll",
                "win/cpu/msvcr120.dll.gz"              : "cpu/win/native/lib/msvcr120.dll",
                "win/cpu/warpctc.dll.gz"               : "cpu/win/native/lib/warpctc.dll",
                "win/cu101/libiomp5md.dll.gz"          : "cu101/win/native/lib/libiomp5md.dll",
                "win/cu101/mkldnn.dll.gz"              : "cu101/win/native/lib/mkldnn.dll",
                "win/cu101/mklml.dll.gz"               : "cu101/win/native/lib/mklml.dll",
                "win/cu101/msvcr120.dll.gz"            : "cu101/win/native/lib/msvcr120.dll",
                "win/cu101/warpctc.dll.gz"             : "cu101/win/native/lib/warpctc.dll",
        ]

        files.each { entry ->
            project.logger.lifecycle("Downloading ${url}/${entry.key}")
            def file = new File("${BINARY_ROOT}/${entry.value}")
            file.getParentFile().mkdirs()
            new URL("${url}/${entry.key}").withInputStream { i -> file.withOutputStream { it << new GZIPInputStream(i) } }
        }

        new File("${BINARY_ROOT}/auto").mkdirs()
    }
}

if (JavaVersion.current() == JavaVersion.VERSION_1_8) {
    tasks.getByName("spotbugsMain").enabled = false
}
